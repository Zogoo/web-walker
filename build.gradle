group = 'com.walker'
version = '0.0.1-SNAPSHOT'

buildscript {
	ext {
		springBootVersion = '1.5.9.RELEASE'
	}
	repositories {
		mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'org.postgresql:postgresql:42.1.4'
        classpath "nu.studer:gradle-jooq-plugin:2.0.9"
        classpath "gradle.plugin.com.boxfuse.client:flyway-release:4.2.0"
	}
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'nu.studer.jooq'
apply plugin: "org.flywaydb.flyway"

sourceSets {
    main.java.srcDir "src/main/java"
    main.resources.srcDir "src/main/resources"
    test.java.srcDir "src/test/java"
    test.resources.srcDir "src/test/resources"
}

jar {
    baseName = 'web-walker'
    version =  '1.0'
}

bootRepackage {
    mainClass = 'com.walker.webwalker.WebWalker'
}

dependencyManagement {
    imports {
        mavenBom 'io.spring.platform:platform-bom:Brussels-SR2'
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "http://repo.spring.io/libs-snapshot" }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies{
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    compile group: 'org.postgresql', name: 'postgresql', version: '42.1.4'
    // JooQ
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jooq'
    compile group: 'org.flywaydb', name: 'flyway-core'
    // jsoup HTML parser library @ https://jsoup.org/
    compile group: 'org.jsoup', name: 'jsoup', version: '1.11.2'
    // for Thymeleaf
    compile group: 'org.thymeleaf', name: 'thymeleaf-spring4'
    // for Lombok
    compile group: 'org.projectlombok', name: 'lombok'

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'
    testCompile group: 'junit', name: 'junit'

    jooqRuntime group: 'org.postgresql', name: 'postgresql', version: '42.1.4'
}

jooq {
    version = '3.10.4'
    edition = 'OSS'
    walker(sourceSets.main) {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = 'jdbc:postgresql://localhost:5432/web_walker'
            user = 'zogoo'
            password = 'admin'
            schema = 'public'
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.postgres.PostgresDatabase'
                inputSchema = 'public'
                customTypes {
                    customType {
                        name = 'com.fasterxml.jackson.databind.JsonNode'
                        converter = 'com.walker.webwalker.util.PostgresJsonBinding'
                    }
                }
                forcedTypes {
                    forcedType {
                        name = 'com.fasterxml.jackson.databind.JsonNode'
                        expression = 'public\\.users\\.address'
                    }
                }
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = false
                fluentSetters = true
            }
            target {
                packageName = 'com.walker.webwalker.dao'
                directory = 'src/main/generated/java'
            }
        }
    }
}

flyway {
    url = 'jdbc:postgresql://localhost:5432/web_walker'
    user = 'zogoo'
    password = 'admin'
    schemas = ['public']
    locations = ["filesystem:$project.projectDir/src/main/resources/db/migration"]
}

generateWalkerJooqSchemaSource.dependsOn flywayMigrate
test.dependsOn flywayClean